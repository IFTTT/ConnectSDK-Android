apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'


android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'


    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 1
        versionName "2.0.0-alpha1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions.unitTests.includeAndroidResources = true
}

dependencies {
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    implementation "com.squareup.moshi:moshi-adapters:$moshiVersion"
    implementation 'androidx.browser:browser:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.0'

    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    testImplementation 'junit:junit:4.12'
    testImplementation "com.google.truth:truth:0.42"
    testImplementation 'org.robolectric:robolectric:4.0'
    testImplementation "com.squareup.retrofit2:retrofit-mock:$retrofitVersion"
}

ext {
    bintrayRepo = 'maven'
    bintrayName = 'ifttt-sdk-android'

    publishedGroupId = 'com.ifttt'
    libraryName = 'ifttt-sdk-android'
    artifact = 'ifttt-sdk-android'

    libraryDescription = 'IFTTT Android SDK is a library that helps facilitate the integration of the IFTTT API.'

    siteUrl = 'https://github.com/IFTTT/IFTTTSDK-Android'
    gitUrl = 'https://github.com/IFTTT/IFTTTSDK-Android.git'

    libraryVersion = '0.0.1'

    developerId = 'lzanita09'
    developerName = 'Zhe Lu'
    developerEmail = 'zhe@ifttt.com'

    licenseName = 'MIT'
    licenseUrl = 'https://github.com/IFTTT/IFTTTSDK-Android/blob/master/LICENSE'
    allLicenses = ['MIT']
}

group = publishedGroupId

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId publishedGroupId
                artifactId artifact

                name libraryName
                description libraryDescription
                url siteUrl

                licenses {
                    license {
                        name licenseName
                        url licenseUrl }
                }
                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

version = libraryVersion

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

afterEvaluate {
    javadoc.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompile.classpath.files
    })
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            gpg {
                sign = true
                passphrase = properties.getProperty("bintray.gpg.password")
            }
        }
    }
}
